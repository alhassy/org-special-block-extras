# Created 2020-04-17 Fri 11:51
#+OPTIONS: toc:nil d:nil
#+OPTIONS: d:nil
#+OPTIONS: html-style:nil html5-fancy:t
#+TITLE: org-special-block-extras
#+AUTHOR: Musa Al-hassy
#+export_file_name: README.org

Twenty-five new custom block and link types for Emacs' Org-mode ^_^

#+toc: headlines 2
#+property: header-args:emacs-lisp :tangle org-special-block-extras.el
#+export_file_name: README
#+macro: blurb Twenty-five new custom block and link types for Emacs' Org-mode ^_^

#+latex_header: \usepackage[hmargin=15mm,top=15mm,bottom=15mm]{geometry}
#+latex_header: \usepackage{newunicodechar}
#+latex_header: \newunicodechar{‚ÇÄ}{\ensuremath{_0}}
#+latex_header: \newunicodechar{‚ÇÅ}{\ensuremath{_1}}
#+latex_header: \newunicodechar{‚ÇÇ}{\ensuremath{_2}}
#+latex_header: \newunicodechar{‚Çô}{\ensuremath{_n}}
#+latex_header: \newunicodechar{·µ¢}{\ensuremath{_i}}
#+latex_header: \newunicodechar{‚Ä≤}{'}
#+latex_header: \newunicodechar{‚áí}{\ensuremath{\Rightarrow}}

#+html_doctype: html5
#+html_head: <meta http-equiv="X-UA-Compatible" content="IE=edge">
#+html_head: <meta name="viewport" content="width=device-width, initial-scale=1">

#+html_head: <link href="http://taopeng.me/org-notes-style/css/notes.css" rel="stylesheet" type="text/css" />

* Example Use
User type something along the lines of the following.
#+begin_example org
,#+begin_red org
/This/
      ,*text*
             _is_
                  red!
,#+end_red
#+end_example

Which generates /red/ text when exported to HTML and LaTeX,
*while supporting Org markup*.

#+begin_quote
This article may be read as a [[https://alhassy.github.io/org-special-block-extras/README.pdf][PDF]] or as [[https://alhassy.github.io/org-special-block-extras/README.html][HTML]] or as pure [[https://alhassy.github.io/org-special-block-extras/README.org][Org]]!
#+end_quote

#+begin_center
( Since we're only considering HTML & LaTeX, the Github rendition of this
article may not render things as desired. )
#+end_center

#+caption: Write Org-markup once, generate for many backends ^_^
[[file:images/colours.jpg]]

#+caption: Displaying thoughts side-by-side ^_^
[[file:images/parallel.png]]

#+caption: ‚ÄúFirst-class editor comments‚Äù In order: Chrome, Emacs Web Wowser, Org source, PDF
[[file:images/edcomm.png]]

#+caption: Visually hiding, folding away, details
[[file:images/details.png]]


The remaining sections are implementation matter.
* Core Utility
#+begin_src emacs-lisp
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Core utility

(defun org-special-block-extras--advice (backend blk contents _)
  "Invoke the appropriate custom block handler, if any.

A given custom block BLK has a TYPE extracted from it, then we
send the block CONTENTS along with the current export BACKEND to
the formatting function ORG-SPECIAL-BLOCK-EXTRAS/TYPE if it is
defined, otherwise, we leave the CONTENTS of the block as is.

We also support the seemingly useless blocks that have no
contents at all, not even an empty new line."
  (let* ((type    (nth 1 (nth 1 blk)))
         (handler (intern (format "org-special-block-extras--%s" type))))
    (ignore-errors (apply handler backend (or contents "") nil))))

(advice-add #'org-html-special-block :before-until
            (-partial #'org-special-block-extras--advice 'html))

(advice-add #'org-latex-special-block :before-until
            (-partial #'org-special-block-extras--advice 'latex))
#+end_src

* Colours

[[file:images/colours.jpg]]

#+begin_src emacs-lisp
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Load support for 19 colour custom blocks

(defvar org-special-block-extras--colors
  '(black blue brown cyan darkgray gray green lightgray lime
          magenta olive orange pink purple red teal violet white
          yellow)
  "Colours that should be available on all systems.")

(loop for colour in org-special-block-extras--colors
      do (eval (read (format
                      "(defun org-special-block-extras--%s (backend contents)
                     (format (pcase backend
                     (`latex \"\\\\begingroup\\\\color{%s}%%s\\\\endgroup\")
                     (`html  \"<div style=\\\"color:%s;\\\">%%s</div>\")
                     (t      \"org-special-block-extras: Unsupported backend\"))
                     contents))"
                      colour colour colour))))
#+end_src

** Examples
#+latex: \newpage
#+begin_black
This text is black!
#+end_black

#+begin_blue
This text is blue!
#+end_blue

#+begin_brown
This text is brown!
#+end_brown

#+begin_cyan
This text is cyan!
#+end_cyan

#+begin_darkgray
This text is darkgray!
#+end_darkgray

#+begin_gray
This text is gray!
#+end_gray

#+begin_green
This text is green!
#+end_green

#+begin_lightgray
This text is lightgray!
#+end_lightgray

#+begin_lime
This text is lime!
#+end_lime

#+begin_magenta
This text is magenta!
#+end_magenta

#+begin_olive
This text is olive!
#+end_olive

#+begin_orange
This text is orange!
#+end_orange

#+begin_pink
This text is pink!
#+end_pink

#+begin_purple
This text is purple!
#+end_purple

#+begin_red
This text is red!
#+end_red

#+begin_teal
This text is teal!
#+end_teal

#+begin_violet
This text is violet!
#+end_violet

#+begin_white
This text is white!
#+end_white

#+begin_yellow
This text is yellow!
#+end_yellow

* Parallel
#+caption: Displaying thoughts side-by-side ^_^
[[file:images/parallel.png]]

#+latex_header: \usepackage{multicol}

#+begin_parallel
_Example:_
#+begin_example org
,#+begin_3parallel org
one

,#+latex: \columnbreak
two

,#+latex: \columnbreak
three
,#+end_3parallel
#+end_example

#+latex: \columnbreak
_Yields:_
#+begin_3parallel
one

#+latex: \columnbreak
two

#+latex: \columnbreak
three
#+end_3parallel
#+end_parallel

| =#+LATEX_HEADER: \usepackage{multicol}= |

I initially used the names =paralell<n>= but names ending with a number did not
inherit highlighting, so I shifted the number to being a prefix instead.
- For LaTeX, new lines are used to suggest opportunities for column breaks
  and are needed even if explicit columnbreaks are declared.

#+begin_src emacs-lisp
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Parallel blocks: parallel<n>[NB] for n:2..5, optionally with ‚ÄòN‚Äôo ‚Äòb‚Äôar
;; in-between the columns.
;;
;; Common case is to have three columns, and we want to avoid invoking the
;; attribute via org, so making this.

(loop for cols in '("1" "2" "3" "4" "5")
      do (loop for rule in '("solid" "none")
      do (eval (read (concat
"(defun org-special-block-extras--" cols "parallel"
(if (equal rule "solid") "" "NB")
"(backend contents)"
"(format (pcase backend"
"(`html \"<div style=\\\"column-rule-style:" rule ";column-count:" cols ";\\\"%s</div>\")"
"(`latex \"\\\\par \\\\setlength{\\\\columnseprule}{" (if (equal rule "solid") "2" "0") "pt}"
"          \\\\begin{minipage}[t]{\\\\linewidth}"
"          \\\\begin{multicols}{" cols "}"
"          %s"
"          \\\\end{multicols}\\\\end{minipage}\")) contents))")))))

(defalias #'org-special-block-extras--parallel   #'org-special-block-extras--2parallel)
(defalias #'org-special-block-extras--parallelNB #'org-special-block-extras--2parallelNB)
#+end_src

#+begin_center
( The [[https://www.gnu.org/software/emacs/manual/html_mono/eww.html][Emacs Web Wowser]], ~M-x eww~, does not display =parallel= environments as
desired. )
#+end_center

* =:argument:= Extraction
#+begin_src emacs-lisp
(defun org-special-block-extras--extract-arguments (contents &rest args)
"Get list of CONTENTS string with ARGS lines stripped out and values of ARGS.

Example usage:

    (-let [(contents‚Ä≤ . (&alist 'k‚ÇÄ ‚Ä¶ 'k‚Çô))
           (‚Ä¶extract-arguments contents 'k‚ÇÄ ‚Ä¶ 'k‚Çô)]
          body)

Within ‚Äòbody‚Äô, each ‚Äòk·µ¢‚Äô refers to the ‚Äòvalue‚Äô of argument
‚Äò:k·µ¢:‚Äô in the CONTENTS text and ‚Äòcontents‚Ä≤‚Äô is CONTENTS
with all ‚Äò:k·µ¢:‚Äô lines stripped out.

+ If ‚Äò:k:‚Äô is not an argument in CONTENTS, then it is assigned value NIL.
+ If ‚Äò:k:‚Äô is an argument in CONTENTS but is not given a value in CONTENTS,
  then it has value the empty string."
  (let ((ctnts contents)
        (values (loop for a in args
                      for regex = (format ":%s:\\(.*\\)" a)
                      for v = (cadr (s-match regex contents))
                      collect (cons a v))))
    (loop for a in args
          for regex = (format ":%s:\\(.*\\)" a)
          do (setq ctnts (s-replace-regexp regex "" ctnts)))
    (cons ctnts values)))
#+end_src

* Editor Comments
‚ÄúEditor Comments‚Äù are intended to be top-level first-class comments in an
article that are inline with the surrounding text and are delimited in such a
way that they are visible but drawing attention.  I first learned about this
idea from Wolfram Kahl ---who introduced me to Emacs many years ago.

In LaTeX, an =edcomm= appears inline with the text surrounding it.
#+begin_edcomm
:ed: Bobert
org-mode is dope, yo!
:replacewith:
Org-mode is essentially a path toward enlightenment.
#+end_edcomm
Unfortunately, in the HTML rendition, the =edcomm= is its own paragraph and thus
separated by new lines from its surrounding text.

#+caption: In order: Chrome, Emacs Web Wowser, Org source, PDF
[[file:images/edcomm.png]]

| /Any new ---possibly empty--- inner lines in the =edcomm= are desirably preserved/ |

#+begin_src emacs-lisp -n -r
(defvar org-special-block-extras-hide-editor-comments nil
  "Should editor comments be shown in the output or not.")

(defun org-special-block-extras--edcomm (backend contents)
"Format CONTENTS as an first-class editor comment according to BACKEND.

The CONTENTS string has two optional argument switches:
1. :ed: ‚áí To declare an editor of the comment.
2. :replacewith: ‚áí [Nullary] The text preceding this clause
   should be replaced by the text after it."
  (-let* (
           ;; Get arguments
           ((contents‚ÇÅ . (&alist 'ed))
            (org-special-block-extras--extract-arguments contents 'ed))

           ;; Strip out any <p> tags     (ref:inline)
           (_ (setq contents‚ÇÅ (s-replace-regexp "<p>" "" contents‚ÇÅ)))
           (_ (setq contents‚ÇÅ (s-replace-regexp "</p>" "" contents‚ÇÅ)))

           ;; Are we in the html backend?
           (html? (equal backend 'html))

           ;; fancy display style
           (boxed (lambda (x)
                    (if html?
                        (concat "<span style=\"border-width:1px"
                                 ";border-style:solid;padding:5px\">"
                                 "<strong>" x "</strong></span>")
                    (concat "\\fbox{\\bf " x "}"))))

           ;; Is this a replacement clause?
           ((this that) (s-split ":replacewith:" contents‚ÇÅ))
           (replacement-clause? that) ;; There is a ‚Äòthat‚Äô
           (replace-keyword (if html? "&nbsp;<u>Replace:</u>"
                              "\\underline{Replace:}"))
           (with-keyword    (if html? "<u>With:</u>"
                              "\\underline{With:}"))
           (editor (format "[%s:%s"
                           (if (s-blank? ed) "Editor Comment" ed)
                           (if replacement-clause?
                               replace-keyword
                             "")))
           (contents‚ÇÇ (if replacement-clause?
                          (format "%s %s %s" this
                                  (funcall boxed with-keyword)
                                  that)
                        contents‚ÇÅ))

           ;; ‚Äú[Editor Comment:‚Äù
           (edcomm-begin (funcall boxed editor))
           ;; ‚Äú]‚Äù
           (edcomm-end (funcall boxed "]")))

    (setq org-export-allow-bind-keywords t) ;; So users can use ‚Äú#+bind‚Äù immediately
    (if org-special-block-extras-hide-editor-comments
        ""
      (format (pcase backend
                (`html "<p> %s %s %s</p>")
                (`latex "%s %s %s"))
              edcomm-begin contents‚ÇÇ edcomm-end))))
#+end_src

#+results:
: org-special-block-extras--edcomm


In the HTML export, the =edcomm= special block is /not/ in-line with the text
surrounding it ---ideally, it would be inline so that existing paragraphs are
not split into multiple paragraphs but instead have an editor's comment
indicating suggested alterations; see [[(inline)][Line (inline)]] above.

** Examples
Org-markup is supported, as expected.

All editor comments are disabled by declaring, in your Org file:
#+begin_example org
,#+bind: org-special-block-extras-hide-editor-comments t
#+end_example
The =#+bind:= keyword makes Emacs variables buffer-local during export
---it is evaluated /after/ any =src= blocks. To use it, one must declare in
their Emacs init file the following line, which our ~edcomm~ utility
ensures is true.
#+begin_src emacs-lisp
(setq org-export-allow-bind-keywords t)
#+end_src

| ( Remember to =C-c C-c= the =#+bind= to activate it, the first time it is written. ) |

#+bind: org-special-block-extras-hide-editor-comments nil

*** No optional arguments
#+begin_edcomm
/Please/ *change* _this_ section to be more, ya know, professional.
#+end_edcomm

*** Only declaring an =:ed:= ---editor
#+begin_edcomm
:ed: Bobert
/Please/ *change* _this_ section to be more, ya know, professional.
#+end_edcomm

#+latex: \vspace{1em}\noindent
Possibly with no contents:
#+begin_edcomm
:ed: Bobert
#+end_edcomm

*** Empty contents, no editor, nothing
#+begin_edcomm
nil#+end_edcomm

#+latex: \vspace{1em}\noindent
Possibly with an empty new line:
#+begin_edcomm

#+end_edcomm

*** With a =:replacewith:= clause
#+begin_edcomm
The two-dimensional notation; e.g., $\sum_{i = 0}^n i^2$
:replacewith:
A linear one-dimensional notation; e.g.,
$(\Sigma i : 0..n \;\bullet\; i^2)$
#+end_edcomm

#+latex: \vspace{1em}\noindent
Possibly ‚Äúmalformed‚Äù replacement clauses.

1. Forget the thing to be replaced.
   #+begin_edcomm
   :replacewith:
   A linear one-dimensional notation; e.g.,
   $(\Sigma i : 0..n \;\bullet\; i^2)$
   #+end_edcomm

2. Forget the new replacement thing.
   #+begin_edcomm
   The two-dimensional notation; e.g., $\sum_{i = 0}^n i^2$
   :replacewith:
   #+end_edcomm

3. Completely lost one's train of thought.
   #+begin_edcomm
   :replacewith:
   #+end_edcomm

* Folded Details
#+latex_header: \makeatletter
#+latex_header: \AtBeginEnvironment{minted}{\dontdofcolorbox}
#+latex_header: \def\dontdofcolorbox{\renewcommand\fcolorbox[4][]{##4}}
#+latex_header: \makeatother


#+latex_header: \newunicodechar{ùìÉ}{\ensuremath{n}}
#+latex_header: \newunicodechar{‚ãØ}{\ensuremath{\cdots}}

- ‚ÄòConversation-style‚Äô articles, where the author asks the reader questions
  whose answers are ‚Äúfolded away‚Äù so the reader can think about the exercise
  before seeing the answer.

- Hiding boring but important code snippets, such as a list of import
  declarations or a tedious implementation.

| =#+LATEX_HEADER: \usepackage{tcolorbox}= |

#+caption: Visually hiding, folding away, details
[[file:images/details.png]]

#+begin_src emacs-lisp -n -r
(defun org-special-block-extras--details (backend contents)
"Format CONTENTS as a ‚Äòfolded region‚Äô according to BACKEND.

CONTENTS may have a ‚Äò:title‚Äô argument specifying a title for
the folded region."
(-let* (;; Get arguments
        ((contents‚Ä≤ . (&alist 'title))
         (org-special-block-extras--extract-arguments contents 'title)))
  (when (s-blank? title) (setq title "Details"))
  (setq title (s-trim title))
  (format
   (s-collapse-whitespace ;; Remove the whitespace only in the nicely presented
                          ;; strings below
    (pcase backend
      (`html "<details class=\"code-details\">
                 <summary>
                   <strong>
                     <font face=\"Courier\" size=\"3\" color=\"green\"> %s
                     </font>
                   </strong>
                 </summary>
                 %s
              </details>")
      (`latex "\\begin{quote}
                 \\begin{tcolorbox}[colback=white,sharp corners,boxrule=0.4pt]
                   \\textbf{%s:}
                   %s
                 \\end{tcolorbox}
               \\end{quote}")))
    title contents‚Ä≤)))
#+end_src

#+latex_header: \usepackage{tcolorbox}

We could use =\begin{quote}\fbox{\parbox{\linewidth}{\textbf{Details:}
...}}\end{quote}=; however, this does not work well with ~minted~, for coloured
source blocks. Instead, we use ~tcolorbox~.

** Example
Reductions ---incidentally also called ‚Äòfolds‚Äô[fn:-1-1]--- embody primitive
recursion and thus computability. For example, what does the following compute
when given a whole number ùìÉ?
#+begin_src emacs-lisp :tangle no
(-reduce #'/ (number-sequence 1.0 ùìÉ))
#+end_src

#+begin_details
:title: Solution
Rather than guess-then-check, let's /calculate/!
#+begin_src emacs-lisp :tangle no
  (-reduce #'/ (number-sequence 1.0 ùìÉ))
= ;; Lisp is strict: Evaluate inner-most expression
  (-reduce #'/ '(1.0 2.0 3.0 ‚Ä¶ ùìÉ))
= ;; Evaluate left-associating reduction
  (/ (/ (/ 1.0 2.0) ‚ãØ) ùìÉ)
=;; Arithmetic: (/ (/ a b) c) = (* (/ a b) (/ 1 c)) = (/ a (* b c))
  (/ 1.0 (* 2.0 3.0 ‚Ä¶ ùìÉ))
#+end_src
We have thus found that Lisp program to compute the inverse factorial of ùìÉ,
i.e., $\frac{1}{ùìÉ!}$.
#+end_details

Neato, let's do more super cool stuff ^_^

#+begin_tiny
(In the Emacs Web Wowser, folded regions are displayed unfolded similar to
LaTeX.)
#+end_tiny

[fn:-1-1] See [[http://www.cs.nott.ac.uk/~pszgmh/fold.pdf][/A tutorial on the universality and expressiveness of fold/]] and
[[http://www.cs.ox.ac.uk/people/jeremy.gibbons/publications/urs.pdf][/Unifying Structured Recursion Schemes/]]
